comment "//" ;
comment "/*" "*/" ;

ProgramExprs.  Program ::= [Expr] ;

separator Expr ";" ;

ConstTrue.  Expr  ::= "true" ;
ConstFalse. Expr  ::= "false" ;
If.         Expr  ::= "if" Expr "then" Expr "else" Expr ;

ConstZero.  Expr  ::= "0" ;
Succ.       Expr  ::= "succ" Expr ;
Pred.       Expr  ::= "pred" Expr ;
IsZero.     Expr  ::= "iszero" Expr ;

Var.          Expr ::= Ident ;
Abstraction.  Expr ::=
  "fun" "(" Ident ":" Type ")" "{" "return" Expr "}" ;
Application.  Expr ::= Expr Expr ;

_. Expr ::= "(" Expr ")" ;

FunType.    Type ::= Type1 "->" Type ;
_.          Type ::= Type1 ;

BoolType.   Type1 ::= "Bool" ;
NatType.    Type1 ::= "Nat" ;
_. Type1 ::= "(" Type ")" ;
